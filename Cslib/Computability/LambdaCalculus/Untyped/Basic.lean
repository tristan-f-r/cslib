/-
Copyright (c) 2025 Fabrizio Montesi. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Fabrizio Montesi
-/

import Cslib.Data.ComputableFresh
import Cslib.Syntax.HasAlphaEquiv
import Cslib.Syntax.Substitution
import Mathlib.Data.Finset.Basic
import Mathlib.Data.Rel

/-! # λ-calculus

The untyped λ-calculus.
-/

universe u

variable {Var : Type u}

namespace LambdaCalculus

/-- Syntax of terms. -/
inductive Term {Var : Type u} : Type u where
| var (x : Var)
| abs (x : Var) (m : @Term Var)
| app (m n : @Term Var)
deriving DecidableEq

/-- Free variables. -/
def Term.fv [DecidableEq Var] (m : @Term Var) : Finset Var :=
  match m with
  | var x => {x}
  | abs x m => m.fv.erase x
  | app m n => m.fv ∪ n.fv

/-- Bound variables. -/
def Term.bv [DecidableEq Var] (m : @Term Var) : Finset Var :=
  match m with
  | var _ => ∅
  | abs x m => m.bv ∪ {x} -- Could also be `insert x m.bv`
  | app m n => m.bv ∪ n.bv

/-- Variable names (free and bound) in a term. -/
def Term.vars [DecidableEq Var] (m : @Term Var) : Finset Var :=
  m.fv ∪ m.bv

/-- Capture-avoiding substitution, as an inference system. -/
inductive Term.Subst [DecidableEq Var] : @Term Var → Var → @Term Var → @Term Var → Prop where
| varHit : (var x).Subst x r r
| varMiss : x ≠ y → (var y).Subst x r (var y)
| absShadow : (abs x m).Subst x r (abs x m)
| absIn : x ≠ y → y ∉ r.fv → m.Subst x r m' → (abs y m).Subst x r (abs y m')
| app : m.Subst x r m' → n.Subst x r n' → (app m n).Subst x r (app m' n')

/-- Renaming, or variable substitution. `m.rename x y` renames `x` into `y` in `m`. -/
def Term.rename [DecidableEq Var] (m : @Term Var) (x y : Var) : @Term Var :=
  match m with
  | var z => if z = x then (var y) else (var z)
  | abs z m' =>
    if z = x then
      -- Shadowing
      abs z m'
    else
      abs z (m'.rename x y)
  | app n1 n2 => app (n1.rename x y) (n2.rename x y)

/-- Renaming preserves size. -/
theorem Term.rename.eq_sizeOf {m : @Term Var} {x y : Var} [DecidableEq Var] : sizeOf (m.rename x y) = sizeOf m := by
  induction m
  case var z =>
    simp only [Term.rename]
    by_cases hzx : z = x <;> simp [hzx]
  case abs z m' ih =>
    simp only [Term.rename]
    by_cases hzx : z = x
    case pos => simp [hzx]
    case neg =>
      simp [hzx]
      apply ih
  case app n1 n2 ih1 ih2 =>
    simp [Term.rename, ih1, ih2]

/-- Capture-avoiding substitution. `m.subst x r` replaces the free occurrences of variable `x` in `m` with `r`. -/
def Term.subst [DecidableEq Var] [ComputableFresh Var] (m : @Term Var) (x : Var) (r : @Term Var) : @Term Var :=
  match m with
  | var y => if y = x then r else var y
  | abs y m' =>
    if y = x then
      abs y m'
    else if y ∉ r.fv then
      abs y (m'.subst x r)
    else
      let z := ComputableFresh.fresh (abs y m').vars
      abs z ((m'.rename y z).subst x r)
  | app m1 m2 => app (m1.subst x r) (m2.subst x r)
termination_by
  sizeOf m
decreasing_by
  · simp
  · rw [Term.rename.eq_sizeOf]
    simp
  · simp
    omega
  · simp
    omega

/-- `Term.subst` is a `Substitution` for λ-terms. -/
instance instSubstitutionTerm [DecidableEq Var] [ComputableFresh Var] :
  Substitution (@Term Var) Var where
  subst := Term.subst

/-- Contexts. -/
inductive Context {Var : Type u} : Type u where
| hole
| abs (x : Var) (c : @Context Var)
| appL (c : @Context Var) (m : @Term Var)
| appR (m : @Term Var) (c : @Context Var)
deriving DecidableEq

/-- Replaces the hole in a `Context` with a `Term`. -/
def Context.fill (c : @Context Var) (m : @Term Var) : @Term Var :=
  match c with
  | hole => m
  | abs x c => Term.abs x (c.fill m)
  | appL c n => Term.app (c.fill m) n
  | appR n c => Term.app n (c.fill m)

/-- Any `Term` can be obtained by filling a `Context` with a variable. This proves that `Context`
completely captures the syntax of terms. -/
theorem Context.complete (m : @Term Var) :
  ∃ (c : @Context Var) (x : Var), m = (c.fill (Term.var x)) := by
  induction m
  case var x =>
    exists hole
    simp [fill]
  case abs x n ih =>
    obtain ⟨c', y, ih⟩ := ih
    exists Context.abs x c'
    exists y
    simp [ih, fill]
  case app n1 n2 ih1 ih2 =>
    obtain ⟨c1, x1, ih1⟩ := ih1
    exists Context.appL c1 n2
    exists x1
    simp [ih1, fill]

open Term

/-- α-equivalence. -/
inductive Term.AlphaEquiv [DecidableEq Var] : Rel (@Term Var) (@Term Var) where
-- The α-axiom
| ax {m : @Term Var} {x y : Var} :
  y ∉ m.fv → AlphaEquiv (abs x m) (abs y (m.rename x y))
-- Equivalence relation rules
| refl : AlphaEquiv m m
| symm : AlphaEquiv m n → AlphaEquiv n m
| trans : AlphaEquiv m1 m2 → AlphaEquiv m2 m3 → AlphaEquiv m1 m3
-- Context closure
| ctx {c : @Context Var} {m n : @Term Var} : AlphaEquiv m n → AlphaEquiv (c.fill m) (c.fill n)

/-- Instance for the substitution notation m[x := n]. -/
instance hasAlphaEquivTerm [DecidableEq Var] : HasAlphaEquiv (@Term Var) where
  AlphaEquiv := Term.AlphaEquiv

end LambdaCalculus
