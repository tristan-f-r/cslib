/-
Copyright (c) 2025 Fabrizio Montesi. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Fabrizio Montesi
-/

import Cslib.Semantics.LTS.Basic
import Cslib.Semantics.LTS.Bisimulation
import Cslib.ConcurrencyTheory.CCS.Basic
import Cslib.ConcurrencyTheory.CCS.Semantics

/-! # Behavioural theory of CCS

## Main results

- `CCS.bisimilarity_congr`: bisimilarity is a congruence in CCS

Additionally, some standard laws of bisimilarity for CCS, including:
- `CCS.bisimilarity_par_nil`: P | ðŸŽ ~ P.
- `CCS.bisimilarity_par_comm`: P | Q ~ Q | P
- `CCS.bisimilarity_choice_comm`: P + Q ~ Q + P
-/

section CCS.BehaviouralTheory

variable {Name : Type u} {Constant : Type v} {defs : Constant â†’ (CCS.Process Name Constant) â†’ Prop}

open CCS CCS.Process CCS.Act

namespace CCS

private inductive ParNil : (Process Name Constant) â†’ (Process Name Constant) â†’ Prop where
| parNil : ParNil (par p nil) p

/-- P | ðŸŽ ~ P -/
theorem bisimilarity_par_nil (p : Process Name Constant) : (par p nil) ~[@lts Name Constant defs] p := by
  exists ParNil
  constructor; constructor
  simp only [Bisimulation]
  intro s1 s2 hr Î¼
  constructor
  case left =>
    intro s1' htr
    cases hr
    cases htr
    case parL p' htr =>
      exists p'
      apply And.intro htr ParNil.parNil
    case parR q' htr =>
      cases htr
    case com Î¼ p' q' htrp htrq =>
      cases htrq
  case right =>
    intro s2' htr
    cases hr
    exists (par s2' nil)
    constructor
    case left =>
      apply Tr.parL htr
    case right =>
      constructor

private inductive ParComm : (Process Name Constant) â†’ (Process Name Constant) â†’ Prop where
| parComm : ParComm (par p q) (par q p)

/-- P | Q ~ Q | P -/
theorem bisimilarity_par_comm (p q : Process Name Constant) : (par p q) ~[@lts Name Constant defs] (par q p) := by
  exists ParComm
  constructor
  case left =>
    constructor
  case right =>
    simp only [Bisimulation]
    intro s1 s2 hr Î¼
    cases hr
    case parComm p q =>
      constructor
      case left =>
        intro t htr
        cases htr
        case parL p' htr' =>
          exists (par q p')
          constructor
          Â· apply Tr.parR htr'
          Â· constructor
        case parR q' htr' =>
          exists (par q' p)
          constructor
          Â· apply Tr.parL htr'
          Â· constructor
        case com Î¼ p' q' htrp htrq =>
          exists (par q' p')
          constructor
          Â· rw [â† Act.co.involution Name Î¼] at htrp
            apply Tr.com htrq htrp
          . constructor
      case right =>
        intro t htr
        cases htr
        case parL q' htr' =>
          exists (par p q')
          constructor
          Â· apply Tr.parR htr'
          Â· constructor
        case parR p' htr' =>
          exists (par p' q)
          constructor
          Â· apply Tr.parL htr'
          Â· constructor
        case com Î¼ p' q' htrp htrq =>
          exists (par q' p')
          constructor
          Â· rw [â† Act.co.involution Name Î¼] at htrp
            apply Tr.com htrq htrp
          . constructor

private inductive ChoiceComm : (Process Name Constant) â†’ (Process Name Constant) â†’ Prop where
| choiceComm : ChoiceComm (choice p q) (choice q p)
| bisim : (p ~[@lts Name Constant defs] q) â†’ ChoiceComm p q

/-- P + Q ~ Q + P -/
theorem bisimilarity_choice_comm : (choice p q) ~[@lts Name Constant defs] (choice q p) := by
  exists @ChoiceComm Name Constant defs
  repeat constructor
  simp only [Bisimulation]
  intro s1 s2 hr Î¼
  cases hr
  rename_i p q
  constructor
  case left =>
    intro s1' htr
    exists s1'
    constructor
    Â· cases htr
      Â· apply Tr.choiceR
        assumption
      Â· apply Tr.choiceL
        assumption
    Â· constructor
      apply Bisimilarity.refl (@lts _ _ defs) s1'
  case right =>
    intro s1' htr
    exists s1'
    constructor
    Â· cases htr
      Â· apply Tr.choiceR
        assumption
      Â· apply Tr.choiceL
        assumption
    Â· constructor
      apply Bisimilarity.refl (@lts _ _ defs) s1'
  case bisim h =>
    constructor
    case left =>
      intro s1' htr
      have hb := Bisimulation.follow_fst (Bisimilarity.is_bisimulation lts) h Î¼ htr
      obtain âŸ¨s2', htr2, hr2âŸ© := hb
      exists s2'
      apply And.intro htr2
      constructor; assumption
    case right =>
      intro s2' htr
      have hb := Bisimulation.follow_snd (Bisimilarity.is_bisimulation lts) h Î¼ htr
      obtain âŸ¨s1', htr1, hr1âŸ© := hb
      exists s1'
      apply And.intro htr1
      constructor; assumption

private inductive PreBisim : (Process Name Constant) â†’ (Process Name Constant) â†’ Prop where
| pre : (p ~[@lts Name Constant defs] q) â†’ PreBisim (pre Î¼ p) (pre Î¼ q)
| bisim : (p ~[@lts Name Constant defs] q) â†’ PreBisim p q

/-- P ~ Q â†’ Î¼.P ~ Î¼.Q -/
theorem bisimilarity_congr_pre : (p ~[@lts Name Constant defs] q) â†’ (pre Î¼ p) ~[@lts Name Constant defs] (pre Î¼ q) := by
  intro hpq
  exists @PreBisim _ _ defs
  constructor; constructor; assumption
  simp only [Bisimulation]
  intro s1 s2 hr Î¼'
  cases hr
  case pre =>
    rename_i p' q' Î¼ hbis
    constructor
    case left =>
      intro s1' htr
      cases htr
      exists q'
      constructor; constructor
      apply PreBisim.bisim hbis
    case right =>
      intro s2' htr
      cases htr
      exists p'
      constructor; constructor
      apply PreBisim.bisim hbis
  case bisim hbis =>
    constructor
    case left =>
      intro s1' htr
      obtain âŸ¨r, hr, hbâŸ© := hbis
      let hbisim := hb
      obtain âŸ¨s2', htr2, hr2âŸ© := hb.follow_fst hr Î¼' htr
      exists s2'
      apply And.intro htr2
      constructor
      apply Bisimilarity.largest_bisimulation _ r hbisim _ _ hr2
    case right =>
      intro s2' htr
      obtain âŸ¨r, hr, hbâŸ© := hbis
      let hbisim := hb
      specialize hb _ _ hr Î¼'
      obtain âŸ¨hb1, hb2âŸ© := hb
      specialize hb2 _ htr
      obtain âŸ¨s1', htr1, hr1âŸ© := hb2
      exists s1'
      apply And.intro htr1
      constructor
      apply Bisimilarity.largest_bisimulation _ r hbisim s1' s2' hr1

private inductive ResBisim : (Process Name Constant) â†’ (Process Name Constant) â†’ Prop where
| res : (p ~[@lts Name Constant defs] q) â†’ ResBisim (res a p) (res a q)
-- | bisim : (p ~[@lts Name Constant defs] q) â†’ ResBisim p q

/-- P ~ Q â†’ (Î½ a) P ~ (Î½ a) Q -/
theorem bisimilarity_congr_res : (p ~[@lts Name Constant defs] q) â†’ (res a p) ~[@lts Name Constant defs] (res a q) := by
  intro hpq
  exists @ResBisim _ _ defs
  constructor; constructor; assumption
  simp only [Bisimulation]
  intro s1 s2 hr Î¼'
  cases hr
  rename_i p q a h
  constructor
  case left =>
    intro s1' htr
    cases htr
    rename_i p' h1 h2 htr
    have h := Bisimulation.follow_fst (Bisimilarity.is_bisimulation lts) h Î¼' htr
    obtain âŸ¨q', htrq, hâŸ© := h
    exists (res a q')
    constructor; constructor; repeat assumption
    constructor; assumption
  case right =>
    intro s2' htr
    cases htr
    rename_i q' h1 h2 htr
    have h := Bisimulation.follow_snd (Bisimilarity.is_bisimulation lts) h Î¼' htr
    obtain âŸ¨p', htrq, hâŸ© := h
    exists (res a p')
    constructor; constructor; repeat assumption
    constructor; assumption

private inductive ChoiceBisim : (Process Name Constant) â†’ (Process Name Constant) â†’ Prop where
| choice : (p ~[@lts Name Constant defs] q) â†’ ChoiceBisim (choice p r) (choice q r)
| bisim : (p ~[@lts Name Constant defs] q) â†’ ChoiceBisim p q

/-- P ~ Q â†’ P + R ~ Q + R -/
theorem bisimilarity_congr_choice : (p ~[@lts Name Constant defs] q) â†’ (choice p r) ~[@lts Name Constant defs] (choice q r) := by
  intro h
  exists @ChoiceBisim _ _ defs
  constructor; constructor; assumption
  simp only [Bisimulation]
  intro s1 s2 r Î¼
  constructor
  case left =>
    intro s1' htr
    cases r
    case choice p q r hbisim =>
      obtain âŸ¨rel, hr, hbâŸ© := hbisim
      cases htr
      case choiceL a b c htr =>
        obtain âŸ¨s2', htr2, hr2âŸ© := hb.follow_fst hr Î¼ htr
        exists s2'
        constructor
        Â· apply Tr.choiceL htr2
        Â· constructor
          apply Bisimilarity.largest_bisimulation _ _ hb _ _ hr2
      case choiceR a b c htr =>
        exists s1'
        constructor
        Â· apply Tr.choiceR htr
        Â· constructor
          apply Bisimilarity.refl
    case bisim hbisim =>
      obtain âŸ¨rel, hr, hbâŸ© := hbisim
      obtain âŸ¨s2', htr2, hr2âŸ© := hb.follow_fst hr Î¼ htr
      exists s2'
      constructor; assumption
      constructor
      apply Bisimilarity.largest_bisimulation _ _ hb _ _ hr2
  case right =>
    intro s2' htr
    cases r
    case choice p q r hbisim =>
      obtain âŸ¨rel, hr, hbâŸ© := hbisim
      cases htr
      case choiceL a b c htr =>
        obtain âŸ¨s1', htr1, hr1âŸ© := hb.follow_snd hr Î¼ htr
        exists s1'
        constructor
        Â· apply Tr.choiceL htr1
        Â· constructor
          apply Bisimilarity.largest_bisimulation _ _ hb _ _ hr1
      case choiceR a b c htr =>
        exists s2'
        constructor
        Â· apply Tr.choiceR htr
        Â· constructor
          apply Bisimilarity.refl
    case bisim hbisim =>
      obtain âŸ¨rel, hr, hbâŸ© := hbisim
      obtain âŸ¨s1', htr1, hr1âŸ© := hb.follow_snd hr Î¼ htr
      exists s1'
      constructor; assumption
      constructor
      apply Bisimilarity.largest_bisimulation _ _ hb _ _ hr1

private inductive ParBisim : (Process Name Constant) â†’ (Process Name Constant) â†’ Prop where
| par : (p ~[@lts Name Constant defs] q) â†’ ParBisim (par p r) (par q r)

/-- P ~ Q â†’ P | R ~ Q | R-/
theorem bisimilarity_congr_par : (p ~[@lts Name Constant defs] q) â†’ (par p r) ~[@lts Name Constant defs] (par q r) := by
  intro h
  exists @ParBisim _ _ defs
  constructor; constructor; assumption
  simp only [Bisimulation]
  intro s1 s2 r Î¼
  constructor
  case left =>
    intro s1' htr
    cases r
    case par p q r hbisim =>
      obtain âŸ¨rel, hr, hbâŸ© := hbisim
      cases htr
      case parL _ _ p' htr =>
        obtain âŸ¨q', htr2, hr2âŸ© := hb.follow_fst hr Î¼ htr
        exists (par q' r)
        constructor
        Â· apply Tr.parL htr2
        Â· constructor
          apply Bisimilarity.largest_bisimulation _ _ hb _ _ hr2
      case parR _ _ r' htr =>
        exists (par q r')
        constructor
        Â· apply Tr.parR htr
        Â· constructor
          apply Bisimilarity.largest_bisimulation _ _ hb _ _ hr
      case com Î¼' p' r' htrp htrr =>
        obtain âŸ¨q', htr2, hr2âŸ© := hb.follow_fst hr Î¼' htrp
        exists (par q' r')
        constructor
        Â· apply Tr.com htr2 htrr
        Â· constructor
          apply Bisimilarity.largest_bisimulation _ _ hb _ _ hr2
  case right =>
    intro s2' htr
    cases r
    case par p q r hbisim =>
      obtain âŸ¨rel, hr, hbâŸ© := hbisim
      cases htr
      case parL _ _ p' htr =>
        obtain âŸ¨p', htr2, hr2âŸ© := hb.follow_snd hr Î¼ htr
        exists (par p' r)
        constructor
        Â· apply Tr.parL htr2
        Â· constructor
          apply Bisimilarity.largest_bisimulation _ _ hb _ _ hr2
      case parR _ _ r' htr =>
        exists (par p r')
        constructor
        Â· apply Tr.parR htr
        Â· constructor
          apply Bisimilarity.largest_bisimulation _ _ hb _ _ hr
      case com Î¼' p' r' htrq htrr =>
        obtain âŸ¨q', htr2, hr2âŸ© := hb.follow_snd hr Î¼' htrq
        exists (par q' r')
        constructor
        Â· apply Tr.com htr2 htrr
        Â· constructor
          apply Bisimilarity.largest_bisimulation _ _ hb _ _ hr2

/-- Bisimilarity is a congruence in CCS. -/
theorem bisimilarity_congr (c : Context Name Constant) (p q : Process Name Constant) (h : p ~[@lts Name Constant defs] q) :
  (c.fill p) ~[@lts Name Constant defs] (c.fill q) := by
  induction c
  case hole =>
    simp only [Context.fill]
    exact h
  case pre Î¼ c ih =>
    simp [Context.fill]
    apply bisimilarity_congr_pre ih
  case parL c r ih =>
    simp [Context.fill]
    apply bisimilarity_congr_par ih
  case parR r c ih =>
    apply Bisimilarity.trans
    Â· apply bisimilarity_par_comm
    Â· apply Bisimilarity.trans
      Â· apply bisimilarity_congr_par
        exact ih
      Â· apply bisimilarity_par_comm
  case choiceL c r ih =>
    simp [Context.fill]
    apply bisimilarity_congr_choice
    exact ih
  case choiceR r c ih =>
    simp [Context.fill]
    apply Bisimilarity.trans
    Â· apply bisimilarity_choice_comm
    Â· apply Bisimilarity.trans
      Â· apply bisimilarity_congr_choice
        exact ih
      Â· apply bisimilarity_choice_comm
  case res =>
    simp [Context.fill]
    apply bisimilarity_congr_res
    assumption

end CCS

end CCS.BehaviouralTheory
